import org.apache.commons.io.FileUtils
import org.apache.tools.ant.taskdefs.condition.Os

class ExecWait extends DefaultTask {
    String command
    String ready
    String directory

    @TaskAction
    def spawnProcess() {

        ProcessBuilder builder = new ProcessBuilder(command.split(' '))
        builder.redirectErrorStream(true)
        builder.directory(new File(directory))
        Process process = builder.start()

        InputStream stdout = process.getInputStream()
        BufferedReader reader = new BufferedReader(new
                InputStreamReader(stdout))

        def line
        while ((line = reader.readLine()) != null) {
            println line
            if (line.contains(ready)) {
                println "$command is ready"
                break;
            }
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "commons-io:commons-io:2.5"
    }
}

plugins {
    id "com.moowork.node" version "0.13"
}

group 'com.friskysoft'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

ext.javaVersion = '1.8'
ext.phantomJsVersion = '2.1.1'

sourceCompatibility = javaVersion

repositories {
    mavenCentral()
    jcenter()
}

project.ext {
    majorVersion = 0
    minorVersion = 0
    patchVersion = 1
    appVersion = String.format('%s.%s.%s-SNAPSHOT', majorVersion, minorVersion, patchVersion)
    project.setVersion(appVersion)
}

def getTimestamp() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

dependencies {
    compile 'org.seleniumhq.selenium:selenium-java:3.+'
    testCompile 'org.testng:testng:6.+'
    testCompile 'io.github.bonigarcia:webdrivermanager:1.+'
    testCompile 'org.slf4j:slf4j-simple:1.+'
    testCompile 'log4j:log4j:1.2.17'

}

clean {
    delete 'screenshots'
}

test {
    useTestNG() {
        parallel 'classes'
        threadCount 5
    }

    testLogging {
        events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat = 'full'
    }

    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            println "\nResults: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        }
    }
}

task updateDrivers(type: Exec) {
    executable = '/bin/bash'
    args = ["scripts/UpdateWebDrivers.sh"]
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

jar {
    manifest {
        attributes("Implementation-Title": "Gradle",
                "Implementation-Version": appVersion)
    }
}

uploadArchives {

    def repoUsername = ""
    def repoPassword = ""
    if (project.hasProperty('ossrhUsername')) {
        repoUsername = ossrhUsername
    }
    if (project.hasProperty('ossrhPassword')) {
        repoPassword = ossrhPassword
    }

    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            //repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            //    authentication(userName: ossrhUsername, password: ossrhPassword)
            //}

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: repoUsername, password: repoPassword)
            }

            pom.project {
                name project.name
                packaging 'jar'
                artifactId project.name
                version appVersion
                groupId project.group
                description project.name

                url 'https://github.com/friskysoft/friskybrowser'

                scm {
                    url 'https://github.com/friskysoft/friskybrowser'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'hossain'
                        name 'Rafaat Hossain'
                        email 'rafaat123@gmail.com'
                    }
                }
            }
        }
    }
}

task downloadPhantomJS {
    def osFilenamePart
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        osFilenamePart = "windows.zip"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        osFilenamePart = "macosx.zip"
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        osFilenamePart = Os.isArch("amd64") ? "linux-x86_64.tar.bz2" : "linux-i686.tar.bz2"
    }

    def filename = "phantomjs-$phantomJsVersion-$osFilenamePart"
    def outputFile = file("$buildDir/webdriver/$filename")
    inputs.property("phantomJsVersion", phantomJsVersion)
    outputs.file(outputFile)

    doLast {
        FileUtils.copyURLToFile(new URL("https://bitbucket.org/ariya/phantomjs/downloads/$filename"), outputFile)
    }
}

task preparePhantomJS(type: Copy) {
    def outputDir = file("$buildDir/webdriver/phantomjs")
    outputs.dir(outputDir)

    def archive = downloadPhantomJS.outputs.files.singleFile

    from(Os.isFamily(Os.FAMILY_MAC) || Os.isFamily(Os.FAMILY_WINDOWS) ? zipTree(archive) : tarTree(archive))
    into(outputDir)
    eachFile { FileCopyDetails fcp ->
        fcp.relativePath = new RelativePath(!fcp.directory, *fcp.relativePath.segments[1..-1])
    }

    def phantomJsFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "phantomjs.exe" : "bin/phantomjs"
    System.setProperty "phantomjs.binary.path", new File(outputDir, phantomJsFilename).absolutePath
    println "phantomjs.binary.path=" + System.getProperty("phantomjs.binary.path")
}

preparePhantomJS.dependsOn "downloadPhantomJS"

tasks.withType(Test) {
    systemProperty "phantomjs.binary.path", System.getProperty("phantomjs.binary.path")
}

task stopNodeServer() {
    def port = 8801
    def cmd = "lsof -Fp -i :$port"
    def process = cmd.execute()
    process.in.eachLine { line ->
        println "${line}"
        println "${line.substring(1)}"
        def killProcess = "kill -9 ${line.substring(1)}".execute()
        killProcess.waitFor()
    }
}

task startNodeServer(type: ExecWait) {
    command 'node server.js'
    ready 'Node server listening at http://localhost'
    directory 'test-website'
}

startNodeServer.dependsOn 'stopNodeServer'
build.finalizedBy(stopNodeServer)